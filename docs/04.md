# Lesson 04

## Create function and send a tip

<!-- ALL-CONTRIBUTORS-BADGE:START - Do not remove or modify this section -->
<!-- ALL-CONTRIBUTORS-BADGE:END -->

Is time to get your hands dirty and start creating some functions to make this contract usable.
As in every other programming language, in Solidity a function is a group of reusable code that can be called anywhere in the program.

To define a function Solidity use the keyword `function` follwed by a unique name, a list of parameters and the curly braces that will sorround the body of the function.

The parameters of the function are declared including the type of the variable received.

A function can have an optional `return` statement. Something required if you want to return some value of the computation made. In Solidity you needd to declare what value (or values) is return from the function by using the `returns ()` keyword in the function "header"2.

Let's see some code

```javascript
// SPDX-License-Identifier: GPL-3.0

pragma solidity ^0.8.4; //same as hardhat.config.js

import 'hardhat/console.sol'; // This allow you to use console.log

contract TipJar {
	uint256 public totalTips; // This will hold/store the number of tips received

    address payable owner; // identify the owner (the address) of the contract

    /*
	 * Store the "Tip" data in a structure
	 * the struct allow you to create a custom datatype
	 */
	struct Tip {
		address sender; //The person who gives you the tip
		string message; //A message from the sender;
		string name; //THe name of the sender
		uint256 timestamp; //When the tip was sent
		uint256 amount; //the amount of ether sent to you
	}
	/*
    store an array of structs to hold all the tips sent
    */
	Tip[] tips;

    constructor() {
        owner = payable(msg.sender); // set the contract createor based in who instantitiated it
    }

	// Functions

	/*
	 * public funtion (like a getter) that returns the total number of tips
	 * is marked as public and as a view, meaning that only reads from the blockchain so is gas free
	 * In a function you should declare what it returns
	 */
	function getTotalTips() public view returns (uint256) {
		return totalTips;
	}

	function sendTip(string memory _message, string memory _name) public payable {
		require(msg.sender.balance >= msg.value, "Youd don't have enough funds"); // require that the sender has enough ether to send
		(bool success, ) = owner.call{value: msg.value}(''); // send the amount of eth specified in msg.value and set the gast limit to 2000 units
		require(success, 'Transfer failed'); // Check that the transfer was successful, if not trigger an error message
		totalTips += 1; //increase the amount of tips
		tips.push(Tip(msg.sender, _message, _name, block.timestamp, msg.value)); // Store the tip
	}

	/*
	 * a function that give access to the stored tips struct
	 * is just read from the blockchain so is marked as view
	 */
	function getAllTips() public view returns (Tip[] memory) {
		return tips;
	}
}

```

Here 3 functions were added, let's review them.

### getTotalTips

This is a very simple function but that use some neat solidity features.
First thing to notice is that it use a visibility modifier. A function (or a variable) can be `public`, `private`,`external` or `internal`.

The `public` keyword indicates that the function meaning that can be accessible from outside of the contract, like the public facing API.

> If you mark a variable as `public` Solidity will create a getter for it to allow other contracts or the javascript client to read the value of it without required a function. In this case if `totalTips` is `public` then, there will be a method named `totalTips()` that will return its value.

> You need to make state variables public if you want other contracts to read them.
> You should make state variables and functions public if you want to be able to read/use them from Javascript client (or other language).

The `private` keyword is to mark a function or variable as only accsible by the same contract.

The `internal` (this is the default modifier) are accesible to the declaring contract an any contracts derived from it.

The `external` defines a function that can be called from other contracts but cannot be called internally).

Check more about Visibility and Getters [in the Solidity Documentation](https://docs.soliditylang.org/en/develop/contracts.html#visibility-and-getters)

The other keyword found in this declaration is `view`.
View functions ensure that they will not modify the state meaning is not:

- Modifiying state variables.
- Emitting events.
- Creating other contracts.
- Using selfdestruct.
- Sentind Ether.
- Calling any function that is not marked as `view` or `pure`.
- Using low-level calls.

Getter methods (the ones automatically created by making a public variable) are `view` function by default.

So in general a `view` function is a read-only function this also means that there are no transactions (since there are no changes in the blockchain) so there are no gas fee by performing this type of functions.

Finally there is the `returns` keyword that define that this function will return a value of type `uint256`

This function all it does is returning the value of the state variable `totalTips`

### getAllTips

This function works in a similar way as the `getTotalTips`, is a `public view` function but this returns an array of `Tip` struct, this way the client of the contract can get the list of `tips` stored in the blockchain.

It also use another keyword, `memory`. You can think on this as analogoues to a computer hard drive (`storage` keyword) and computers RAM (`memory`). Memory is a temporary place to store some data between function calls.

Eveery transaction on the blockchain cost some Gass. The lower the gas consumption of the code the better. The gas consumption of `memory` is insignificat in comparison to its counterpart. The `memory` variable will exists only while the function is being called.

### sendTip

This is the core of the contract, the function that perform the transaction between the sender and you - the owner - .

This function accepts 2 arguments, the message and the name, both strings (this use a common practice of naming arguments with an underscore prefix). Is also `public payable` function.

Inside of it you'll found new things too, first this line

```javascript
require(msg.sender.balance >= msg.vale, "You don't have enough funds");
```

Solidity offers a serie of global variables that are available to be used by any function. Some of that are `msg.value` and `msg.sender`.

`msg.sender` makes reference to the address of the caller of the function
`msg.value` It contains the amount of `wei` (eth / 1e18) sent in the transaction. This means that `msg.value` will store the amount of ether sent with that `payable` function. (this amount will be set by the javascript client that will call the `sendTip` function)

Other memebers of the `msg` object are

```javascript
	msg.data (bytes): complete calldata
	msg.gas (uint): remaining gas - deprecated in version 0.4.21 and to be replaced by gasleft()
	msg.sig (bytes4): first four bytes of the calldata (i.e. function identifier)
```

#### require

This is a call to `require` method. This is part of a set of function designed to rever state changes to prevent possible issues.

`require` accepts two arguments: a condition and a message if the condition fails. By using it you let Solidity to deal with the errors by reverting the state modifications and throwing exceptions.
This function guarantees validity of the conditions. It can be used to check inpuits, contract state variables and return values from other calls.

In this case you'll use `require` to check if the value that the sender want to sent is lower that the balance of the sender account.

#### Give me the money!

Next in the code you'll see the following statement.

```javascript
(bool success, ) = owner.call{ value: msg.value}('')
```

First, at the left side you'll found a destructuring assignment, works in the same way as in javascript.
Solidity the use of tuples, a list of objects of potentially different types, this are used to be able to return multiple values at the same time.

> Be aware the tuples are not a propert type, they can only be used to form groupings of expressions

You can assign the return value of a function to a variable, in this case the function returns a tuple where the first element of it is a boolean so you can assign that to the tuple where the first element will be named `success`.

Then, at the righ side there is a function call in this case is calling the `call` method on the `owner` address.

But first, let's check the different ways to send ETH to some address.

1. `address.send(amount)` This method can be used to send `amount` of ETH to `address` but have some considerations

- The unsuccessful execution of `send()` returns `false` but do not throw an exception. So you need to pay attention when to use this (like using some constraints to check the operation) because even an unsuccessful operation will spend some gas.

- It have a gas limit of 2300 units.

2. `address.transfer(amount)` Is a newer method to send `amount` of ETH to `address`. Things to consider:

- Same limit of gas as before 2300 units, but there is a `gas` modifier that allow you to define the gas limit of the operation.
- If the operation fails, an exception will be throw allowing the wallet to notify about it.
- It required that the receiving contract to implement a `fallback` function.

3. `address.call{value: amount}()`: This is the most customizable way of sending `amount` to `address`. This is the recommended way after December 2019. It will forward all gas or set gas.
   This method is the recommended way to avoid Re-entrancy attacks, if you want to know more about it check [this article by OpenZeppeling](https://blog.openzeppelin.com/reentrancy-after-istanbul/)

This function call will read the value of `msg.value`. The empty argument `('')` if the way to trigger the fallback function of the receiving address.

> Similar to how one dollar is equal to 100 cents
> one ETH is equal to 10^18 wei
> wei is the smallest uint of ether.
> In Solidity you can directly use some units like wei and ether
> uint public oneWei = 1 wei;
> bool isOneWei = 1 wei == 1;
> uint oneEther = 1 ether;
> bool isOneEther = 1 ethere == 1e18;

#### Update the tips array

After the transfer of money is susccess you need to update the `totalTips` variable to reflect the correct number and lastly update the `tips` array by creating a new struct with the correct data

`Tip(msg.sender, _message, _name, block.timestamp, msg.value)`

This piece of code will create a new `Tip` struct with the address of `msg.sender`, the `_message` and `_name` passed as argumentos to the function, the `block.timestamp` to get the timestamp of the current block in seconds since epoch and finally the amount of `eth` sent by the user that resides in `msg.value`.

And Voila!, You just send money to the owner of the contract.